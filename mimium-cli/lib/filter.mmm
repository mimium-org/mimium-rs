include("math.mmm")
fn onepole(x, ratio){
    x*(1.0-ratio) + self*ratio
}

fn smooth(x){
    onepole(x, 0.95)
}

fn gen_hp_coeff(fc,q,fs){
    let omega = 2.0 * PI* fc / fs
    let alpha = sin(omega)/(2.0* q)
    let norm = 1.0 + alpha
    let a1 = -2.0 * cos(omega) / norm
    let a2 = (1.0 - alpha)/ norm
    let btmp = 1.0 + cos(omega)
    let b0 = btmp / (2.0*norm)
    let b1 = -btmp / norm
    let b2 = b0
    (a1,a2,b0,b1,b2)
}
fn gen_lp_coeff(fc,q,fs){
    let omega = 2.0 * PI* fc / fs
    let alpha = sin(omega)/(2.0* q)
    let norm = 1.0 + alpha
    let a1 = -2.0 * cos(omega) / norm
    let a2 = (1.0 - alpha)/ norm
    let btmp = 1.0 - cos(omega)
    let b0 = btmp / (2.0*norm)
    let b1 = btmp  / norm
    let b2 = b0
    (a1,a2,b0,b1,b2)
}

fn gen_bp_coeff(fc,q,fs){
    let omega = 2.0 * PI* fc / fs
    let alpha = sin(omega)/(2.0* q)
    let norm = 1.0 + alpha
    let a1 = -2.0 * cos(omega) / norm
    let a2 = (1.0 - alpha)/ norm
    let b0 = sin(omega) / (2.0*norm)
    let b1 = 0
    let b2 = -b0
    (a1,a2,b0,b1,b2)
}

fn gen_ap_coeff(fc,q,fs){
    let omega = 2.0 * PI* fc / fs
    let alpha = sin(omega)/(2.0* q)
    let norm = 1.0 + alpha
    let a1 = -2.0 * cos(omega) / norm
    let a2 = (1.0 - alpha)/ norm
    let b0 = a2
    let b1 = a1
    let b2 = 1.0
    (a1,a2,b0,b1,b2)
}

fn gen_peaking_coeff(fc,gain_db,q,fs){
    let A = 10 ^ (gain_db/40)
    let omega = 2.0 * PI* fc / fs
    let alpha = sin(omega)/(2.0* q)
    let norm = 1 + alpha/A
    let b0 = 1.0
    let b1 = -2*cos(omega)/norm
    let b2 = (1 - alpha * A) / norm
    let a1 = b1
    let a2 = (1 - alpha /A)/norm
    (a1,a2,b0,b1,b2)
}

fn _biquad_inner (x,a1,a2){
    let (ws, wss, _wsss) = self
    let w = x - a1*ws - a2*wss
    (w, ws, wss)
}
fn biquad(x,coeffs){
    let (a1,a2,b0,b1,b2) = coeffs;
	let (w,ws,wss) = _biquad_inner(x,a1,a2);
	b0*w + b1*ws + b2*wss
}

fn lowpass(x,fc,q){
    biquad(x,gen_lp_coeff(fc,q,samplerate))
}

fn highpass(x,fc,q){
    biquad(x,gen_hp_coeff(fc,q,samplerate))
}
fn bandpass(x,fc,q){
    biquad(x,gen_bp_coeff(fc,q,samplerate))
}
fn allpass(x,fc,q){
    biquad(x,gen_ap_coeff(fc,q,samplerate))
}

fn peakeq(x,fc,gaindb,q){
    biquad(x,gen_peaking_coeff(fc,gaindb,q,samplerate))
}